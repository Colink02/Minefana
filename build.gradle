plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'java'
    id 'maven'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group project.pluginGroup
    version project.pluginVersion

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-oss-repo'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }

        maven {
            name = 'jitpack.io'
            url = 'https://jitpack.io/'
        }

        jcenter()
    }

    dependencies {
        compile 'org.influxdb:influxdb-java:2.9'

        // Platform independent configuration loading
        compile 'net.md-5:bungeecord-config:1.12-SNAPSHOT'

        // Easier logging API
        compile 'org.slf4j:slf4j-jdk14:1.7.25'

        //GEO IP database
        compile 'com.github.games647:MaxMind-DB-Reader-java:2.0.1'

        // Decompressing tar archive from maxmind
        compile 'javatar:javatar:2.5'

        // Available in all platforms
        compile 'com.google.guava:guava:17.0'
    }

    shadowJar {
        dependencies {
            // shadow task doesn't support transitive dependencies, so we have to add them explicitly
            include(project(':minefana-common'))

            // Required dependencies that aren't included
            // GEO-IP
            include(dependency('com.github.games647:MaxMind-DB-Reader-java'))

            // decompressing
            include(dependency('javatar:javatar'))

            // Influx
            include(dependency('org.influxdb:influxdb-java'))
            include(dependency('com.squareup.okio:okio'))
            include(dependency('com.squareup.moshi:moshi'))
            include(dependency('com.squareup.retrofit2:retrofit'))
            include(dependency('com.squareup.retrofit2:converter-moshi'))
            include(dependency('com.squareup.okhttp3:okhttp'))
            include(dependency('com.squareup.okhttp3:logging-interceptor'))

            include(dependency('net.md-5:bungeecord-config'))

            include(dependency('org.slf4j:slf4j-jdk14'))
            include(dependency('org.slf4j:slf4j-api'))
        }
    }
}
